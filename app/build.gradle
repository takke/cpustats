apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    //noinspection GroovyAssignabilityCheck
    signingConfigs {
        myConfig
    }

    compileSdkVersion rootProject.ext.compileSdkVersion as Integer
    buildToolsVersion rootProject.ext.buildToolsVersion as String

    defaultConfig {
        applicationId 'jp.takke.cpustats'
        targetSdkVersion rootProject.ext.targetSdkVersion as Integer
        minSdkVersion rootProject.ext.minSdkVersion as Integer
        versionCode rootProject.ext.versionCode as Integer
        versionName rootProject.ext.versionName as String
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.myConfig
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix 'd'

            // use Built-in Shrinker instead of ProGuard
//            minifyEnabled true
//            useProguard false
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("release")) {

            variant.outputs.each { output ->
                if (output.outputFileName.endsWith('.apk')) {
                    def shortVersionName = defaultConfig.versionName.replaceAll('\\.', '')
                    def d = new java.text.SimpleDateFormat("yyyyMMdd_HHmm").format(new Date())
                    def newName = "CpuStats_r${defaultConfig.versionCode}_${shortVersionName}_${d}.apk"
                    output.outputFileName = newName
                }
            }
        }
    }

    flavorDimensions "edition"

    productFlavors {
        quad3 {
            dimension "edition"
        }
        quad5 {
            dimension "edition"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:support-v4:${supportLibraryVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta5'

    testImplementation "org.robolectric:robolectric:${robolectricVersion}"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.apache.maven:maven-ant-tasks:2.1.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

// load signing settings from gradle.properties
if (project.hasProperty('storeFile')) {
    android.signingConfigs.myConfig.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.myConfig.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.myConfig.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.myConfig.keyPassword = keyPassword
}