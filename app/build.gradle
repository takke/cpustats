apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    //noinspection GroovyAssignabilityCheck
    signingConfigs {
        myConfig
    }

    compileSdkVersion rootProject.ext.compileSdkVersion as Integer
    buildToolsVersion rootProject.ext.buildToolsVersion as String

    defaultConfig {
        applicationId 'jp.takke.cpustats'
        targetSdkVersion rootProject.ext.targetSdkVersion as Integer
        minSdkVersion rootProject.ext.minSdkVersion as Integer
        versionCode rootProject.ext.versionCode as Integer
        versionName rootProject.ext.versionName as String

        // Rename APK/AAB filename
        def shortVersionName = versionName.replaceAll('\\.', '')
        //noinspection UnnecessaryQualifiedReference
        def d = new java.text.SimpleDateFormat("yyyyMMdd_HHmm").format(new Date())
        setProperty("archivesBaseName", "${apkNamePrefix}_${shortVersionName}_${d}")
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.myConfig
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix 'd'
        }
    }

    flavorDimensions "edition"

    productFlavors {
        quad3 {
            dimension "edition"
        }
        quad5 {
            dimension "edition"
        }
    }

    // publish task
    def publish = project.tasks.create("publishAll")
    applicationVariants.all { variant ->

        if (variant.buildType.name == "release") {

            variant.outputs.each { output ->
                if (output.outputFileName.endsWith('.apk')) {

                    // publishXxxApk
                    def task = project.tasks.create("publish${variant.name.capitalize()}Apk", Copy)

                    def srcDir = variant.packageApplicationProvider.get().outputDirectory
                    def srcPath = new File(srcDir, output.outputFileName)
                    task.from(srcPath)
                    task.into(deployTo)
                    task.dependsOn variant.assembleProvider.get()
                    publish.dependsOn task
                }
            }
        }
    }

    // bundlePublishXxxRelease task
    applicationVariants.all { variant ->

        if (variant.buildType.name == "release") {
            def flavorName0 = variant.productFlavors[0].name
            def buildTypeName = variant.buildType.name

            def task = project.tasks.create("bundlePublish${variant.name.capitalize()}", Copy)

            def path = "${buildDir}/outputs/bundle/${variant.name}/${archivesBaseName}-${flavorName0}-${buildTypeName}.aab"
//            System.println("*** aab path = $path ($task) [$flavorName0] [$buildTypeName]")

            task.from(path)
            task.into(deployTo)
            task.dependsOn "bundle${variant.name.capitalize()}"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:${appcompat_version}"
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.annotation:annotation:$annotation_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"

    testImplementation "org.robolectric:robolectric:${robolectricVersion}"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.apache.maven:maven-ant-tasks:2.1.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

// load signing settings from gradle.properties
if (project.hasProperty('storeFile')) {
    android.signingConfigs.myConfig.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.myConfig.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.myConfig.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.myConfig.keyPassword = keyPassword
}